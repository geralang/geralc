
mod std::vec

use std::iter
use std::str

pub proc new() {
    return {
        content = [],
        size = 0
    }
}

proc grow(vector, default_value, new_alloc_size) {
    var new_content = array(default_value, new_alloc_size)
    0..length(vector.content) |> iter::for_each(|i| {
        new_content[i] = vector.content[i]
    })
    vector.content = new_content
}

pub proc push(vector, item) {
    var new_size = vector.size + 1
    case length(vector.content) == 0 -> vector |> grow(item, 16)
    else case new_size > length(vector.content) ->
        vector |> grow(item, length(vector.content) * 2)
    vector.content[vector.size] = item
    vector.size = new_size
    return vector
}

pub proc append_array(vector, appended_array) {
    case length(appended_array) == 0 -> return vector
    var new_size = vector.size + length(appended_array)
    case new_size > length(vector.content) ->
        vector |> grow(appended_array[0], new_size)
    0..length(appended_array) |> iter::for_each(|i| {
        vector.content[vector.size + i] = appended_array[i]
    })
    vector.size = new_size
    return vector
}

pub proc append_vector(vector, appended_vector) {
    case appended_vector.size == 0 -> return vector
    var new_size = vector.size + appended_vector.size
    case new_size > length(vector.content) ->
        vector |> grow(appended_vector.content[0], new_size)
    0..appended_vector.size |> iter::for_each(|i| {
        vector.content[vector.size + i] = appended_vector.content[i]
    })
    vector.size = new_size
    return vector
}

pub proc insert(vector, index, item) {
    var new_size = vector.size + 1
    case new_size > length(vector.content) ->
        vector |> grow(item, length(vector.content) * 2)
    mut var idx = index
    case index < 0 -> idx = index + vector.size
    case idx > vector.size -> panic(
        "the insertion index _ is out of bounds for a vector of length _"
            |> str::format(idx)
            |> str::format(vector.size)
    )
    idx..vector.size |> iter::for_each(|i| {
        vector.content[i + 1] = vector.content[i]
    })
    vector.content[idx] = item
    vector.size = new_size
    return vector
}

pub proc pop(vector) {
    vector.size = vector.size - 1
    return vector
}

pub proc remove(vector, index) {
    mut var idx = index
    case index < 0 -> idx = index + vector.size
    case idx >= vector.size -> panic(
        "the index _ is out of bounds for a vector of length _"
            |> str::format(idx)
            |> str::format(vector.size)
    )
    vector.size = vector.size - 1
    idx..vector.size |> iter::for_each(|i| {
        vector.content[i] = vector.content[i + 1]
    })
    return vector
}

pub proc get(vector, index) {
    mut var idx = index
    case index < 0 -> idx = index + vector.size
    case idx >= vector.size -> panic(
        "the index _ is out of bounds for a vector of length _"
            |> str::format(idx)
            |> str::format(vector.size)
    )
    return vector.content[idx]
}

pub proc set(vector, index, value) {
    mut var idx = index
    case index < 0 -> idx = index + vector.size
    case idx >= vector.size -> panic(
        "the index _ is out of bounds for a vector of length _"
            |> str::format(idx)
            |> str::format(vector.size)
    )
    vector.content[idx] = value
    return vector
}

pub proc iter(vector) {
    mut var i = 0
    return || {
        case i >= vector.size -> return #end unit
        var val = vector.content[i]
        i = i + 1
        return #next val
    }
}

pub proc collect(from_iter) {
    var dest = new()
    from_iter |> iter::for_each(|item| dest |> push(item))
    return dest 
}